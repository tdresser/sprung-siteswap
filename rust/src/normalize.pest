crossed_inverted = { "ci" | "ic" }
inverted = {"i"}
crossed = {"c"}
natural = {"n"}

bottom_natural = { "B" }
bottom_opposite = { "b" }
top_natural = { "T" }
top_opposite = { "t" }
position_pair_item = _{ bottom_natural | bottom_opposite | top_natural | top_opposite }
position_pair = { position_pair_item ~ position_pair_item }

position = _{ crossed_inverted | inverted | crossed | natural | position_pair }

zip_position = { position }
arc_position = { position }
infix_arc_position = { position }

digit = { '0'..'9' } // Looks like this needs an extra wrapper? Gross.
C = {"C"}
F = {"F"}
B = {"B"}

ambiguous_position = { crossed | inverted | crossed_inverted }

notation = { 
    SOI ~ (
    ((zip_position ~ "z")* ~ (arc_position ~ "a")* ~ "S" ~ ( // explicit arcs
        digit+ | C | F)) | 
    ((zip_position ~ "z")* ~ "S" ~ ( // infix arcs
        (infix_arc_position? ~ digit)+ | 
        (infix_arc_position? ~ (C | F)))) |
    (ambiguous_position ~ "S" ~ (// ambiguous arcs
        digit+ | C | F)) |
    ((zip_position ~ "z")* ~ (arc_position ~ "a")* ~ B) | // explicit arcs (box)
    (ambiguous_position ~ B)// ambiguous arcs (box)
    ) ~ EOI
}